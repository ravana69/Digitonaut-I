<!DOCTYPE html>
<html lang="en">

<!-- Mirrored from digitonaut.com/art/ice_dreamer/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:26:08 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset='utf-8'\>
<meta name='apple-mobile-web-app-capable' content='yes'><meta name='apple-mobile-web-app-status-bar-style' content='black-translucent'><title>Ice Dreamer</title>

<script src="shader-web-background.min.js"></script>

<script type="x-shader/x-fragment" id="image">
precision highp float;
uniform vec2  iResolution;
uniform float iTime;
uniform sampler2D iChannel0;

 // -------------------------------------------------------------------------------------------------- Paste your Shadertoy code here:

// Created by Vinicius Graciano Santos - vgs/2015
// https://www.shadertoy.com/view/XlSGDK

#define STEPS 100
#define EPS 0.02
#define FAR 100.0
#define PI 3.14159265359

// smin by iq
float smin( float a, float b, float k )
{
    float h = clamp(0.5+0.5*(b-a)/k, 0.0, 1.0 );
    return mix(b, a, h) - k*h*(1.0-h);
}

float map(vec3 p) {
    p = mod(p, 10.0) - 5.0;
    return smin(length(p)-1.25, min(length(p.yz), 
                min(length(p.xz), length(p.xy))), 0.5)-1.0;
}

vec3 grad(vec3 p) {
    vec2 q = vec2(0.0, EPS);
    return vec3(map(p + q.yxx) - map(p - q.yxx),
                map(p + q.xyx) - map(p - q.xyx),
                map(p + q.xxy) - map(p - q.xxy));
}

// Cube and bump mapping by iq
vec3 cubeMap(vec3 p, vec3 n) {
    vec3 a = texture2D(iChannel0, 0.1*p.yz).rgb;
    vec3 b = texture2D(iChannel0, 0.1*p.xz).rgb;
    vec3 c = texture2D(iChannel0, 0.1*p.xy).rgb;
    n = abs(n);
    return (a*n.x + b*n.y + c*n.z)/(n.x+n.y+n.z);   
}

vec3 bumpMap(vec3 p, vec3 n, float c) {
    vec2 q = vec2(0.0, 0.25);
	vec3 grad = -(vec3(cubeMap(p+q.yxx, n).r, cubeMap(p+q.xyx, n).r, cubeMap(p+q.xxy, n).r)-c)/q.y;
    vec3 t = grad - n*dot(grad, n);
    return normalize(n - t);
}

vec3 shade(vec3 ro, vec3 rd, float t) {
    vec3 p = ro + t*rd;
    vec3 n = normalize(grad(p));
    vec3 tex = cubeMap(p, n);
    n = bumpMap(p, n, tex.r);
    
    vec3 col = 3.0*vec3(0.3, 0.5, 0.7)*tex*(pow(1.0-dot(-rd, n), 4.0)*.6 + .4*dot(-rd, n));
    float fog = 1.0 - exp(-0.01*t);
    return mix(col, vec3(1.0), fog);
}

void mainImage( out vec4 fragColor, in vec2 fragCoord ) {
    
	vec2 uv = (-iResolution.xy + 2.0*fragCoord.xy) / iResolution.y;
    vec2 vi = fragCoord.xy/iResolution.xy;
    
    vec3 ro = vec3(0.0, 0.0, 2.5+iTime);
    vec3 rd = normalize(vec3(uv, -1.0));
    
    float t = 0.0, d = 0.0;
    for (int i = 0; i < STEPS; ++i) {
        d = .55*map(ro + t*rd);
        if (d < EPS || t > FAR) break;
        t += d;
        rd.xy = cos(0.05*d)*rd.xy + sin(0.05*d)*vec2(-rd.y, rd.x);
    }
	
    vec3 col = d < EPS ? shade(ro, rd, t) : vec3(1.0);
	col += 1.0-vec3(1.0)*pow(vi.x*vi.y*(1.0-vi.x)*(1.0-vi.y), 0.03);
    
    col = max(col-0.26, 0.0);
	col = (col*(6.2*col+.5))/(col*(6.2*col+1.7)+0.06);
    
    col = pow(col, vec3(1.0/2.2));    
	fragColor = vec4(col,1.0);
}
 // -------------------------------------------------------------------------------------------------- End of Shadertoy code

void main() {
	mainImage(gl_FragColor, gl_FragCoord.xy);
}
</script>
<script>
shaderWebBackground.shade({
	onInit: (ctx) => {
		const image = new Image();
		image.crossOrigin = "anonymous";
		image.src = "ichannel0.jpg";
		image.onload = () => {
			const gl = ctx.gl;
			const texture = gl.createTexture();
			gl.bindTexture(gl.TEXTURE_2D, texture);
			  
			/* repeat texture */
			gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MIN_FILTER, gl.NEAREST);
			gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MAG_FILTER, gl.NEAREST);
			gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_WRAP_S, gl.REPEAT);
			gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_WRAP_T, gl.REPEAT);  
			
			gl.texImage2D(gl.TEXTURE_2D, 0, gl.RGBA, gl.RGBA, gl.UNSIGNED_BYTE, image);
			ctx.iChannel0 = texture;
		}
	},
	onResize: (width, height, ctx) => {
		ctx.minDimension = Math.min(width, height);
	},
	shaders: {
		image: {
			uniforms: {
				iResolution: (gl, loc, ctx) => gl.uniform2f(loc, ctx.width, ctx.height),
				iTime: (gl, loc, ctx) => gl.uniform1f(loc, performance.now() / 1000),
				iChannel0: (gl, loc, ctx) => ctx.texture(loc, ctx.iChannel0)
			}
		}
	}
});
</script>
</head>
<body>
</body>

<!-- Mirrored from digitonaut.com/art/ice_dreamer/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:26:11 GMT -->
</html>
